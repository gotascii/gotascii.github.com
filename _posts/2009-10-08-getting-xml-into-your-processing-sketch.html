--- 
layout: post
title: Getting XML Into Your Processing Sketch
excerpt: <p>During one of the recent Learning Processing sessions, <a href="http://www.keithmuth.net/">Keith</a> asked how one would go about getting data from a web service, such as <a href="http://developer.yahoo.com/yql/">YQL</a>, into a Processing sketch.  After poking around the Processing <a href="http://processing.org/reference/libraries/">library reference</a> we were able to create an example of how to pull in data from a web service and use it to control the parameters of a sketch.</p><img src="/images/flickr.png"/>
---
<p>It turns out getting XML data into Processing from a web service is pretty easy.  The first thing we need to do is import the built-in XML library.</p>

{% highlight java %}import processing.xml.*;{% endhighlight %}

<p>Next we construct our query string using a <a href="http://developer.yahoo.com/yql/">YQL</a> query that returns the most interesting flickr photos.  You can take a look at the result here <a href="http://tinyurl.com/ydqc4rt">select * from flickr.photos.interestingness(10)</a>.  One of the cooler aspects of XMLElement is that you can initialize an object with a url and it will take care of making the http request and fetching the XML.</p>


{% highlight java linenos %}
String query = "http://query.yahooapis.com/v1/public/yql?q=select%20*%20from%20flickr.photos.interestingness(" + count + ")&format=xml";
XMLElement xml = new XMLElement(this, query);
{% endhighlight %}

<p>There are two children returned in the XML: diagnostics and results.  We want to examine the data in results, so we get that block of XML via the <a href="http://processing.org/reference/XMLElement_getChild_.html">getChild</a> method.  Children are indexed starting at 0, so the second child is at index 1.</p>

{% highlight java %}XMLElement results = xml.getChild(1);{% endhighlight %}

<p>At this point we can stop and party for a while because we have technically achieved our goal. We have pulled some data into our sketch and it is sitting inside our XMLElement object. The next step is to iterate through the data and do something with it.</p>
<p>Before we started to code we took some time to analyze the data and figure out a few different ways to display it.  I noticed that with each record flickr returned a server id.  We assumed this identified the server that contains the picture.  I was curious as to how distributed the results were and we decided to write a sketch based on this aspect of the data.  One of the most interesting things to do in Processing is to find new ways of looking at data in order to expose hidden characteristics.</p>
<p>The first thing we'll do is iterate through the records using a for loop. The <a href="http://processing.org/reference/XMLElement_getChildCount_.html">getChildCount</a> method allows us to define the for loop conditions and we use <a href="http://processing.org/reference/XMLElement_getChild_.html">getChild</a> inside the loop to access individual results.  The <a href="http://processing.org/reference/XMLElement_getStringAttribute_.html">getStringAttribute</a> lets us get specific attribute values from a particular record so we'll use that to get back the server id.</p>

{% highlight java %}
for(int i = 0; i < results.getChildCount(); i++) {
  XMLElement result = results.getChild(i);
  String server = result.getStringAttribute("server");
}
{% endhighlight %}

<p>We tie all together by first checking to see if we've created an entry in our HashMap.  If we haven't, we create one and put a 1 in the counter.  If we find an entry already exists, we increment the counter stored at that key and save it.</p>

{% highlight java %}
HashMap hm = new HashMap();
for(int i = 0; i < results.getChildCount(); i++) {
  if(hm.get(server) == null) {
    hm.put(server, 1);
  } else {
    Integer sum = (Integer)hm.get(server);
    sum += 1;
    hm.put(server, sum);
  }
}
{% endhighlight %}

<p>When the loop finishes running we'll have a HashMap with keys that correspond to server ids and values that contain the number of images that server holds.  We took that data and made a simple processing sketch that creates ellipses which are larger and more red if they are serving more images.  In the end, the sketch turned out to be a strange sort of load monitoring visualization for flickr.  You can get the code for this sketch <a href="http://github.com/gotascii/processing/blob/master/notes/xml/xml.pde">here</a>.  Here is the full code for our HashMap creation method.</p>
{% highlight java %}
import processing.xml.*;

HashMap serverCounts(int count) {
  String query = "http://query.yahooapis.com/v1/public/yql?q=select%20*%20from%20flickr.photos.interestingness(" + count + ")&format=xml";
  XMLElement xml = new XMLElement(this, query);
  XMLElement results = xml.getChild(1);

  HashMap hm = new HashMap();
  for(int i = 0; i < results.getChildCount(); i++) {
    XMLElement result = results.getChild(i);
    String server = result.getStringAttribute("server");
    if(hm.get(server) == null) {
      hm.put(server, 1);
    } else {
      Integer sum = (Integer)hm.get(server);
      sum += 1;
      hm.put(server, sum);
    }
  }
  return hm;
}
{% endhighlight %}
